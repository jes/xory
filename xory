#!/usr/bin/perl
# X or Y game
# James Stanley 2013

use strict;
use warnings;

use AnyEvent::Util;
use DBM::Deep;
use Image::Magick;
use Mojolicious::Lite;
use POSIX qw(tmpnam);

my $db = DBM::Deep->new('xory.db');

helper 'baseurl' => sub {
    return 'http://xory.pm';
};

get '/' => sub {
    my $self = shift;
    $self->redirect_to(url_for_id(1));
    #$self->render('index');
};

get '/about' => sub {
    my $self = shift;
    $self->render('about');
};

get '/play/:id' => sub {
    my $self = shift;
    $self->redirect_to(url_for_id($self->param('id')));
};

get '/play/:id/:name' => sub {
    my $self = shift;
    my $id = $self->param('id');
    if ($self->param('name') ne gamename($id)) {
        $self->redirect_to(url_for_id($id));
        return;
    }

    my $gamecfg = $self->session("cfg$id") || initial_config($id);
    $gamecfg = initial_config($id) if $gamecfg->{delete_cfg};
    my $idx = $gamecfg->{idx};
    $self->session("cfg$id" => $gamecfg);

    $self->stash(
        cat1 => $db->{game}{$id}{cat1},
        cat2 => $db->{game}{$id}{cat2},
        descr => $db->{game}{$id}{descr},
        imgurl => "/img/game/$id/$idx.jpg",
        gameid => $id,
        idx => $idx,
        npictures => $db->{game}{$id}{npictures},
    );
    $self->render('play');
};

get '/choice/:id/:idx/:cat' => sub {
    my $self = shift;

    my $id = $self->param('id');
    my $idx = $self->param('idx');

    my $gamecfg = $self->session("cfg$id") || {};
    $gamecfg->{choices}{$idx} = $self->param('cat') == 1 ? $db->{game}{$id}{cat1} : $db->{game}{$id}{cat2};
    $gamecfg->{idx} = $idx + 1;
    $self->session("cfg$id" => $gamecfg);

    if ($gamecfg->{idx} <= $db->{game}{$id}{npictures}) {
        $self->redirect_to("/play/$id/" . gamename($id));
    } else {
        $self->redirect_to("/finish/$id/" . gamename($id));
    }
};

get '/finish/:id' => sub {
    my $self = shift;
    $self->redirect_to(url_for_id($self->param('id'), 'finish'));
};

get '/finish/:id/:name' => sub {
    my $self = shift;
    my $id = $self->param('id');
    if ($self->param('name') ne gamename($id)) {
        $self->redirect_to(url_for_id($id, 'finish'));
        return;
    }

    my $gamecfg = $self->session("cfg$id") || {};
    $gamecfg->{delete_cfg} = 1;
    $self->session("cfg$id" => $gamecfg);

    my @status;
    my $ncorrect = 0;
    for my $i (1 .. $db->{game}{$id}{npictures}) {
        push @status, {
            idx => $i,
            imgurl => "/img/game/$id/$i.jpg",
            category => $db->{game}{$id}{category}[$i-1],
            yousaid => $gamecfg->{choices}{$i},
        };
        $ncorrect++ if $db->{game}{$id}{category}[$i-1] eq $gamecfg->{choices}{$i};
    }

    $self->stash(
        descr => $db->{game}{$id}{descr},
        gameid => $id,
        gamename => gamename($id),
        status => \@status,
        npictures => $db->{game}{$id}{npictures},
        ncorrect => $ncorrect,
    );
    $self->render('finish');
};

get '/create' => sub {
    my $self = shift;
    $self->render('create');
};

post '/create' => sub {
    my $self = shift;

    if (!defined $self->param('cat1') || !defined $self->param('cat2')) {
        $self->flash(error => 'Please enter categories.');
        $self->redirect_to('/create');
        return;
    }

    my $id = ++$db->{ngames};

    $db->{game}{$id} = {
        cat1 => $self->param('cat1'),
        cat2 => $self->param('cat2'),
        category => [],
        descr => $self->param('descr'),
        npictures => 0,
    };

    give_perm($self, $id);

    $self->redirect_to(url_for_id($id, 'add-pics'));
};

get '/add-pics/:id' => sub {
    my $self = shift;
    $self->redirect_to(url_for_id($self->param('id'), 'add-pics'));
};

get '/add-pics/:id/:name' => sub {
    my $self = shift;
    my $id = $self->param('id');
    if ($self->param('name') ne gamename($id)) {
        $self->redirect_to(url_for_id($id, 'add-pics'));
        return;
    }

    if (!has_perm($self, $id)) {
        $self->flash(error => 'Insufficient permission to edit that game.');
        $self->redirect_to('/create');
        return;
    }

    my @pictures;
    for my $i (1 .. $db->{game}{$id}{npictures}) {
        push @pictures, {
            idx => $i,
            imgurl => "/img/game/$id/$i.jpg",
            category => $db->{game}{$id}{category}[$i-1],
        };
    }

    $self->stash(
        cat1 => $db->{game}{$id}{cat1},
        cat2 => $db->{game}{$id}{cat2},
        gameid => $id,
        gamename => gamename($id),
        npictures => $db->{game}{$id}{npictures},
        pictures => \@pictures,
    );
    $self->render('add-pics');
};

# TODO: auth
get '/wizard' => sub {
    my $self = shift;
    $self->session(wizard => 1);
    $self->redirect_to('/');
};

post '/add-pic' => sub {
    my $self = shift;
    my $id = $self->param('gameid');

    if (!defined $id || $id !~ /^\d+$/) {
        $self->flash(error => 'Something went wrong.');
        $self->redirect_to('/create');
        return;
    }

    if (!has_perm($self, $id)) {
        $self->flash(error => 'Insufficient permission to edit that game.');
        $self->redirect_to('/create');
        return;
    }

    my $category = $self->param('category');
    my $imgfile = $self->req->upload('img');

    fork_call {
        handle_uploaded_pic($id, $category, $imgfile);
    } sub {
        if ($@) {
            print STDERR "$@";
            $self->flash(error => 'Something went wrong while processing that image. Sorry!');
        }
        $self->redirect_to(url_for_id($id, 'add-pics'));
    };
};

post '/delete-pic' => sub {
    my $self = shift;
    my $id = $self->param('gameid');

    if (!defined $id || $id !~ /^\d+$/) {
        $self->flash(error => 'Something went wrong.');
        $self->redirect_to('/create');
        return;
    }

    if (!has_perm($self, $id)) {
        $self->flash(error => 'Insufficient permission to edit that game.');
        $self->redirect_to('/create');
        return;
    }

    my $idx = $self->param('idx');
    if (!defined $idx || $id !~ /^\d+$/) {
        $self->flash(error => 'Something went wrong.');
        $self->redirect_to(url_for_id($id, 'add-pics'));
        return;
    }

    for my $i ($idx .. $db->{game}{$id}{npictures}-1) {
        my $i1 = $i+1;
        rename "public/game/$id/$i1.jpg", "public/game/$i.jpg";
        $db->{game}{$id}{category}[$i-1] = $db->{game}{$id}{category}[$i];
    }
    pop @{ $db->{game}{$id}{category} };
    $db->{game}{$id}{npictures}--;

    $self->redirect_to(url_for_id($id, 'add-pics'));
};

app->start;

sub url_for_id {
    my $id = shift;
    my $type = shift || 'play';

    return "/$type/$id/" . gamename($id);
}

sub initial_config {
    my $id = shift;

    return {
        idx => 1,
        choices => {},
    };
}

sub gamename {
    my $id = shift;

    my $cat1 = $db->{game}{$id}{cat1};
    my $cat2 = $db->{game}{$id}{cat2};

    $cat1 =~ s/[^a-zA-Z0-9_]//g;
    $cat2 =~ s/[^a-zA-Z0-9_]//g;

    return "$cat1-or-$cat2";
}

sub handle_uploaded_pic {
    my ($id, $category, $img) = @_;

    mkdir "public/img/game/$id";

    my $tmp_path = tmpnam();
    $img->move_to($tmp_path);

    my $im = Image::Magick->new();
    my $err = $im->Read($tmp_path);
    die $err if $err;

    $err = $im->Scale(geometry => '450x350');
    die $err if $err;

    my $idx = $db->{game}{$id}{npictures} + 1;
    $err = $im->Write("public/img/game/$id/$idx.jpg");
    die $err if $err;

    push @{ $db->{game}{$id}{category} }, $category;
    $db->{game}{$id}{npictures}++;
}

sub give_perm {
    my ($self, $id) = @_;

    my $perms = $self->session('perms') || {};
    $perms->{$id} = 1;
    $self->session(perms => $perms);
}

sub has_perm {
    my ($self, $id) = @_;

    my $perms = $self->session('perms') || {};
    return $perms->{$id} || $self->session('wizard');
}
